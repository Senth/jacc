apply plugin: 'maven'
ext.artifactId = "java-channels"

task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}


configurations {
	deployerJars
	provided
}

sourceSets {
	main { compileClasspath += configurations.provided }
}

dependencies {
	compile 'com.spiddekauga.http:http-url-utils:0.1'
	compile 'org.json:json:20090211'
	deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}


install  {
	repositories.mavenInstaller {
		pom.artifactId = project.artifactId
	}
}

uploadArchives {
	repositories {
		// Set mavenRepositoryUrl & mavenUserName inside gradle.properties
		mavenDeployer {
			configuration = configurations.deployerJars
			pom.artifactId = project.artifactId
			repository(url: mavenRepositoryUrl + '/releases') {
				authentication(userName: mavenUserName)
			}
			snapshotRepository(url: mavenRepositoryUrl + '/snapshots') {
				authentication(userName: mavenUserName)
			}
		}
	}
}

eclipse.project {
	name = 'java-channels-api'
}
